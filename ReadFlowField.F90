!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                                                         ! 
!    FILE: ReadFlowField.F90                              !
!    CONTAINS: subroutine ReadFlowField                   !
!                                                         ! 
!    PURPOSE: Initialization routine. Reads in a flow     !
!     snapshot generated by a previous simulation, and    !
!     interpolates to the current grid if necessary       !
!                                                         !
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      subroutine ReadFlowField
      use mpih
      use decomp_2d
      use local_arrays
      use param
      IMPLICIT NONE
      real ::  aaa
      character*70 :: filnam,dsetname
      integer :: ihist
      integer :: nthom,nzom,nro,nzo,ntho
      integer :: xs2og,xe2og,xs3og,xe3og
      integer :: istro
      real :: stro
      real :: intinfo(1:4)
      real, allocatable, dimension(:,:,:) :: qrold,qzold,qtold
      logical :: fexist

      
!EP   Reading old grid information by master
      filnam = trim('continua_master.h5')

      inquire(file=filnam,exist=fexist)
      if (.not.fexist) then 
        write(*,*) 'Continuation files not found'
        call MpiAbort
      end if

      if (ismaster) then
       dsetname = trim('nth')
       call HdfSerialReadIntScalar(dsetname,filnam,ntho)
       dsetname = trim('nr')
       call HdfSerialReadIntScalar(dsetname,filnam,nro)
       dsetname = trim('nz')
       call HdfSerialReadIntScalar(dsetname,filnam,nzo)
       dsetname = trim('time')
       call HdfSerialReadRealScalar(dsetname,filnam,time)
       dsetname = trim('istr')
       call HdfSerialReadIntScalar(dsetname,filnam,istro)
       dsetname = trim('str')
       call HdfSerialReadRealScalar(dsetname,filnam,stro)
      endif
      
      call MpiBarrier
      call MpiBcastInt(ntho)
      call MpiBcastInt(nro)
      call MpiBcastInt(nzo)
      call MpiBcastInt(istro)
      call MpiBcastReal(stro)
      call MpiBcastReal(time)

!EP   Check whether grid specifications have been updated
      if(nzo.ne.nz.or.nro.ne.nr.or.ntho.ne.nth &
       .or.istro.ne.istr.or.stro.ne.str) then
       if(ismaster) write(*,*) "Interpolating new grid"

       if(nth.gt.ntho*2.or.nz.gt.nzo*2.or.nr.gt.nro*2) then

       if(ismaster) write(*,*) "New grid resolution cannot be more",&
            "than twice the old resolution"

       call MpiAbort

      endif

      nthom = ntho - 1
      nzom = nzo - 1
      
      intinfo(1) = istro
      intinfo(2) = stro

      call MpiBarrier

      xs2og = floor(real(xstart(2)*nzom/nzm))
      xe2og = ceiling(real(xend(2)*nzom/nzm))
      xs3og = floor(real(xstart(3)*nthom/nthm))
      xe3og = ceiling(real(xend(3)*nthom/nthm))

      xs2og = max(xs2og,1)
      xe2og   = min(xe2og,nzom)
      xs3og = max(xs3og,1)
      xe3og   = min(xe3og,nthom)
      
!EP   qt
      allocate(qtold(0:nro+1,xs2og-lvlhalo:xe2og+lvlhalo, & 
           xs3og-lvlhalo:xe3og+lvlhalo))
      
      call HdfReadContinua(ntho,nzo,nro,xs2og,xe2og, &
       xs3og,xe3og,1,qtold(1:nro,xs2og-lvlhalo:xe2og+lvlhalo, &
       xs3og-lvlhalo:xe3og+lvlhalo))

      call interp(qtold,qt(1:nr,xstart(2):xend(2),xstart(3):xend(3)) &
       ,ntho,nzo,nro,istro,stro,1,xs2og,xe2og,xs3og,xe3og)

      deallocate(qtold)

!EP   qr
      allocate(qrold(0:nro+1,xs2og-lvlhalo:xe2og+lvlhalo, & 
           xs3og-lvlhalo:xe3og+lvlhalo))
      
      call HdfReadContinua(ntho,nzo,nro,xs2og,xe2og, &
       xs3og,xe3og,2,qrold(1:nro,xs2og-lvlhalo:xe2og+lvlhalo, &
       xs3og-lvlhalo:xe3og+lvlhalo))

      call interp(qrold,qr(1:nr,xstart(2):xend(2),xstart(3):xend(3)) &
       ,ntho,nzo,nro,istro,stro,2,xs2og,xe2og,xs3og,xe3og)

      deallocate(qrold)

!EP   qz
      allocate(qzold(0:nro+1,xs2og-lvlhalo:xe2og+lvlhalo, & 
           xs3og-lvlhalo:xe3og+lvlhalo))
      
      call HdfReadContinua(ntho,nzo,nro,xs2og,xe2og, &
       xs3og,xe3og,3,qzold(1:nro,xs2og-lvlhalo:xe2og+lvlhalo, &
       xs3og-lvlhalo:xe3og+lvlhalo))

      call interp(qzold,qz(1:nr,xstart(2):xend(2),xstart(3):xend(3)) &
       ,ntho,nzo,nro,istro,stro,3,xs2og,xe2og,xs3og,xe3og)

      deallocate(qzold)

      else

      call HdfReadContinua(nth,nz,nr,xstart(2),xend(2) &
       ,xstart(3),xend(3),1,qt)
      call HdfReadContinua(nth,nz,nr,xstart(2),xend(2) &
       ,xstart(3),xend(3),2,qr)
      call HdfReadContinua(nth,nz,nr,xstart(2),xend(2) &
       ,xstart(3),xend(3),3,qz)

      endif

      if (resetlogstime) time=0.

!EP   Increase the maximum simulation time by the end time in the
!continuation files

      tmax = tmax + time

      return                                                            
      end                                                               
